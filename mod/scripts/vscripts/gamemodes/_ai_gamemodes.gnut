global function AiGameModes_Init

global function AiGameModes_SetNPCWeapons

global function AiGameModes_SpawnDropShip
global function AiGameModes_SpawnDropPod
global function AiGameModes_SpawnReaper
global function AiGameModes_SpawnTitan

global function GetValidIntroDropShipSpawn
// new modified function
global function ValidateNPCSpawnpoints

const INTRO_DROPSHIP_CUTOFF = 2000

struct
{
	table< string, array<string> > npcWeaponsTable // npcs have their default weapon in aisettings file
} file

void function AiGameModes_Init()
{
	
}

//------------------------------------------------------

void function AiGameModes_SetNPCWeapons( string npcClass, array<string> weapons )
{
	if ( !( npcClass in file.npcWeaponsTable ) )
		file.npcWeaponsTable[ npcClass ] <- []
	file.npcWeaponsTable[ npcClass ] = weapons
}

//------------------------------------------------------

void function AiGameModes_SpawnDropShip( vector pos, vector rot, int team, int count, void functionref( array<entity> guys ) squadHandler = null )
{  
	string squadName = MakeSquadName( team, UniqueString( "" ) )

	CallinData drop
	drop.origin 		= pos
	drop.yaw 			  = rot.y
	drop.dist 			= 768
	drop.team 			= team
	drop.squadname 	= squadName
	SetDropTableSpawnFuncs( drop, CreateSoldier, count )
	// shall we make npcs always drop ignoring other stuffs...?
	// oh welp, eDropStyle.FORCED can't do anything to avoid spawning failure
	SetCallinStyle( drop, eDropStyle.ZIPLINE_NPC )
	//SetCallinStyle( drop, eDropStyle.FORCED )
  
	thread RunDropshipDropoff( drop )
	
	WaitSignal( drop, "OnDropoff" )
	
	array< entity > guys = GetNPCArrayBySquad( squadName )
	
	foreach ( guy in guys )
	{
		SetUpNPCWeapons( guy )
		// no need to setup ai here...
		//guy.EnableNPCFlag( NPC_ALLOW_PATROL | NPC_ALLOW_INVESTIGATE | NPC_ALLOW_HAND_SIGNALS | NPC_ALLOW_FLEE )
	}
	
	if ( squadHandler != null )
		thread squadHandler( guys )
}


void function AiGameModes_SpawnDropPod( vector pos, vector rot, int team, string content /*( ͡° ͜ʖ ͡°)*/, void functionref( array<entity> guys ) squadHandler = null, int droppodFlag = 0 )
{
	entity pod = CreateDropPod( pos, <0,0,0> )
	
	InitFireteamDropPod( pod, droppodFlag )
		
	waitthread LaunchAnimDropPod( pod, "pod_testpath", pos, rot )

	string squadName = MakeSquadName( team, UniqueString( "" ) )
	array<entity> guys
	for ( int i = 0; i < SQUAD_SIZE; i++ )
	{
		entity npc = CreateNPC( content, team, pos,<0,0,0> )
		DispatchSpawn( npc )
		SetSquad( npc, squadName )
		
		SetUpNPCWeapons( npc )
		
		npc.SetParent( pod, "ATTACH", true )
		
		// no need to setup ai here...
		//npc.EnableNPCFlag( NPC_ALLOW_PATROL | NPC_ALLOW_INVESTIGATE | NPC_ALLOW_HAND_SIGNALS | NPC_ALLOW_FLEE )
		guys.append( npc )
	}
	
	ActivateFireteamDropPod( pod, guys )

	// start searching for enemies
	if ( squadHandler != null )
		thread squadHandler( guys )
}

const float REAPER_WARPFALL_DELAY = 4.7 // same as fd does
void function AiGameModes_SpawnReaper( vector pos, vector rot, int team, string aiSettings = "", void functionref( entity reaper ) reaperHandler = null )
{
	float reaperLandTime = REAPER_WARPFALL_DELAY + 1.2 // reaper takes ~1.2s to warpfall
	// damageDef parameter has been removed because vanilla doesn't seem to have that behavior
	//thread HotDrop_Spawnpoint( pos, team, reaperLandTime, false, damagedef_reaper_fall )
	thread HotDrop_Spawnpoint( pos, team, reaperLandTime, false )

	wait REAPER_WARPFALL_DELAY
	entity reaper = CreateSuperSpectre( team, pos, rot )
	reaper.EndSignal( "OnDestroy" )

	// setup spawn
	SetSpawnOption_Warpfall( reaper )
	if ( aiSettings != "" )
		SetSpawnOption_AISettings( reaper, aiSettings )
	
	HideName( reaper ) // prevent flash a name onto it
	reaper.kv.VisibilityFlags = ENTITY_VISIBLE_TO_NOBODY // prevent it's model light shows up
	DispatchSpawn( reaper )
	reaper.kv.VisibilityFlags = ENTITY_VISIBLE_TO_EVERYONE // restore visibility after warpfall starts in spawncallbacks
	// fake a spawntime to delay replay tracking, hide it's spawn
	//reaper.ai.spawnTime = Time() + 0.5
	// now we have modified settings in _base_gametype_mp.gnut to allow us offset replay start time
	SetEntityReplayStartTimeOffset( reaper, 0.5 ) // this means we delay their replay by 0.5s!

	// reaper highlight
	Highlight_SetFriendlyHighlight( reaper, "sp_friendly_pilot" )
	reaper.Highlight_SetParam( 1, 0, HIGHLIGHT_COLOR_FRIENDLY )
	reaper.e.hasDefaultEnemyHighlight = true // mark them as hasDefaultHighlight, so they'll save highlights after phase shift
	SetDefaultMPEnemyHighlight( reaper )
	Highlight_SetEnemyHighlight( reaper, "enemy_titan" )

	reaper.WaitSignal( "WarpfallComplete" )
	ShowName( reaper ) // show name again after drop
	
	if ( reaperHandler != null )
		thread reaperHandler( reaper )
}

// copied from cl_replacement_titan_hud.gnut
// damageDef parameter has been removed because vanilla doesn't seem to have that behavior
//void function HotDrop_Spawnpoint( vector origin, int team, float impactTime, bool hasFriendlyWarning = false, int damageDef = -1 )
void function HotDrop_Spawnpoint( vector origin, int team, float impactTime, bool hasFriendlyWarning = false )
{
	array<entity> targetEffects = []
	vector surfaceNormal = < 0, 0, 1 >

	int index = GetParticleSystemIndex( $"P_ar_titan_droppoint" )

	if( hasFriendlyWarning || FriendlyFire_IsEnabled() ) // also warn friendly players, if we enabled friendly fire...
	{
		entity effectFriendly = StartParticleEffectInWorld_ReturnEntity( index, origin, surfaceNormal )
		SetTeam( effectFriendly, team )
		EffectSetControlPointVector( effectFriendly, 1, FRIENDLY_COLOR_FX )
		effectFriendly.kv.VisibilityFlags = ENTITY_VISIBLE_TO_FRIENDLY
		effectFriendly.DisableHibernation() // prevent it from fading out
		targetEffects.append( effectFriendly )
	}

	entity effectEnemy = StartParticleEffectInWorld_ReturnEntity( index, origin, surfaceNormal )
	SetTeam( effectEnemy, team )
	EffectSetControlPointVector( effectEnemy, 1, ENEMY_COLOR_FX )
	effectEnemy.kv.VisibilityFlags = ENTITY_VISIBLE_TO_ENEMY
	effectEnemy.DisableHibernation() // prevent it from fading out
	targetEffects.append( effectEnemy )

	// so enemy npcs will mostly avoid them
	// welp, vanilla attrition doesn't seem to have such behavior
	// guess I'll leave it for only my modified contents( extra_ai_spawner.gnut )
	/*
	entity damageAreaInfo
	if ( damageDef > -1 )
	{
		damageAreaInfo = CreateEntity( "info_target" )
		DispatchSpawn( damageAreaInfo )
		damageAreaInfo.SetOrigin( origin )
		AI_CreateDangerousArea_DamageDef( damageDef, damageAreaInfo, team, true, true )
	}
	*/

	wait impactTime

	// clean up
	foreach( entity targetEffect in targetEffects )
	{
		if ( IsValid( targetEffect ) )
			EffectStop( targetEffect )
	}
	/*
	if ( IsValid( damageAreaInfo ) )
		damageAreaInfo.Destroy()
	*/
}


const float TITANFALL_WARNING_DURATION = 5.0
const float TITAN_WARPFALL_DELAY = 2.5 // warpfall sound + fx + animation lasts 2.5s, add 2.5s more delay
// including aisettings stuff specifically for at bounty titans
void function AiGameModes_SpawnTitan( vector pos, vector rot, int team, string setFile, string aiSettings = "", void functionref( entity titan ) titanHandler = null )
{
	// damageDef parameter has been removed because vanilla doesn't seem to have that behavior
	//thread HotDrop_Spawnpoint( pos, team, TITANFALL_WARNING_DURATION, false, damagedef_titan_fall )
	thread HotDrop_Spawnpoint( pos, team, TITANFALL_WARNING_DURATION, false )

	// warpfall variant
	if ( Flag( "LevelHasRoof" ) )
	{
		wait TITAN_WARPFALL_DELAY
		waitthread NPCPrespawnWarpfallSequence( aiSettings, pos, rot )
	}

	entity titan = CreateNPCTitan( setFile, team, pos, rot )
	// normal titanfall
	if ( !Flag( "LevelHasRoof" ) )
		SetSpawnOption_Titanfall( titan )
	
	if ( aiSettings != "" )
		SetSpawnOption_AISettings( titan, aiSettings )
	
	DispatchSpawn( titan )

	// warpfall variant
	if ( Flag( "LevelHasRoof" ) )
	{
		thread PlayFXOnEntity( TURBO_WARP_COMPANY, titan, "offset" )
		thread NPCTitanHotdrops( titan, true, "at_hotdrop_drop_2knee_turbo_upgraded" )
	}
	
	if ( titanHandler != null )
		thread titanHandler( titan )
}

void function SetUpNPCWeapons( entity guy )
{
	string className = guy.GetClassName()
	
	array<string> mainWeapons
	if ( className in file.npcWeaponsTable )
		mainWeapons = file.npcWeaponsTable[ className ]
	
	if ( mainWeapons.len() == 0 ) // no valid weapons
		return

	// take off existing main weapons, or sometimes they'll have a archer by default
	foreach ( entity weapon in guy.GetMainWeapons() )
		guy.TakeWeapon( weapon.GetWeaponClassName() )

	if ( mainWeapons.len() > 0 )
	{
		string weaponName = mainWeapons[ RandomInt( mainWeapons.len() ) ]
		guy.GiveWeapon( weaponName )
		guy.SetActiveWeaponByName( weaponName )
	}
}

// Checks if we can spawn a dropship at a node, this should guarantee dropship ziplines
array<entity> function GetValidIntroDropShipSpawn( array<entity> introNodes )
{
	array<entity> introShipSpawns
	
	if ( GetZiplineDropshipSpawns().len() == 0 )
		return []
	
	array<entity> validDropshipNodes = GetZiplineDropshipSpawns()
	foreach ( node in introNodes )
	{
		// try to add each node only once
		//entity closestNode = GetClosest( GetZiplineDropshipSpawns(), node.GetOrigin() )
		entity closestNode = GetClosest( validDropshipNodes, node.GetOrigin() )
		// try not to assign team if we failed distance checks?
		//SetTeam( closestNode, node.GetTeam() )
		
		// never append same node multiple times
		if ( introShipSpawns.contains( closestNode ) )
			continue

		if ( Distance( closestNode.GetOrigin(), node.GetOrigin() ) < INTRO_DROPSHIP_CUTOFF )
		{
			SetTeam( closestNode, node.GetTeam() )
			introShipSpawns.append( closestNode )
			validDropshipNodes.removebyvalue( closestNode )
		}
	}
	
	return introShipSpawns
}

void function ValidateNPCSpawnpoints( array<entity> points, string npcToSpawn = "npc_soldier" )
{
	entity npcDummy = CreateNPC( npcToSpawn, TEAM_BOTH, <0,0,0>, <0,0,0> )
	// have to dispatchspawn before clamping points
	DispatchSpawn( npcDummy )
	array<entity> invalidSpawns // remove spawns later after we done foreach, otherwise
	foreach ( point in points )
	{
		vector origin = point.GetOrigin()
		npcDummy.SetOrigin( origin )
		// function from _base_gametype.gnut, could fix everything!
		if ( IsPointOutOfBounds( origin ) )
		{
			points.fastremovebyvalue( point ) // have to use fastremovebyvalue othewise "foreach" will offset incorrectly from next element in array
			//print( string( point ) + " at " + string( origin ) + " is out of battle! filtering" )
			continue
		}
		// search radius from mp_weapon_frag_drone.nut
		vector ornull clampedPos = NavMesh_ClampPointForAIWithExtents( origin, npcDummy, < 20, 20, 36 > )
		if ( clampedPos == null )
		{
			points.fastremovebyvalue( point ) // have to use fastremovebyvalue othewise "foreach" will offset incorrectly from next element in array
			//print( string( point ) + " at " + string( origin ) + " isn't covered by " + npcToSpawn + "'s navmesh, filtering" )
			continue
		}
	}
	if ( IsValid( npcDummy ) )
		npcDummy.Destroy()
}