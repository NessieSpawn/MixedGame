global function RespawnProtection_Init

void function RespawnProtection_Init(){
	RegisterSignal( "RespawnProtectionEnd" )
	AddCallback_OnPlayerRespawned( OnPlayerRespawned )
}

void function OnPlayerRespawned( entity player )
{
	thread RespawnProtection(player,GetCurrentPlaylistVarFloat("respawnprotection",0.0))
}

void function RespawnProtection( entity player , float duration ){
	OnThreadEnd(
		function() : ( player )
		{
			if( IsValid( player ) )
			{
				player.Signal( "RespawnProtectionEnd" )
				SetDefaultMPEnemyHighlight( player )
				//cancel protect if player attack
				if( IsDemigod( player ) )
					DisableDemigod( player );
			}
		}
	)
	
	player.EndSignal( "OnDeath" )
	player.EndSignal( "OnDestroy" )
	player.EndSignal( "OnPrimaryAttack" )
	player.EndSignal( "OnMelee" ) // nessie modified signal, sent on melee attach active

	if( IsDemigod( player ) )
	{
		DisableDemigod( player );
	}
	else{
		//start protect
		EnableDemigod( player )
		//start a highlight
		waitthread ProectionHighlightThink( player, duration )
	}
}

void function ProectionHighlightThink( entity player, float duration )
{
	player.EndSignal( "OnDeath" )
	player.EndSignal( "OnDestroy" )
	player.EndSignal( "RespawnProtectionEnd" )

	float startTime = Time()
	while( Time() - startTime <= duration )
	{
		Highlight_SetEnemyHighlight( player, "sp_enemy_pilot" )
		player.Highlight_SetParam( 2, 0, < 255,215,0 > ) // gold highlight

		WaitFrame()

		if ( player.IsTitan() )
			break

		// weapon checks to break protection early
		entity activeWeapon = player.GetActiveWeapon()
		if ( IsValid( activeWeapon ) )
		{
			// trying to use ability or cook grenade
			if ( activeWeapon.IsWeaponOffhand() )
				break

			// trying to charge up weapon
			if ( activeWeapon.IsChargeWeapon() && activeWeapon.GetWeaponChargeFraction() > 0 )
				break

			// trying to lock with weapon
			if ( activeWeapon.GetWeaponSettingBool( eWeaponVar.attack_button_presses_ads ) && player.GetZoomFrac() == 1 )
				break
		}
	}
}
