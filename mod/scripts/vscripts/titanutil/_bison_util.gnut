untyped // for player.GetPlayerSettingsMods()
global function BisonUtil_Init

const array<string> BISON_DAMAGE_REDUCTION_MELEE_MODS =
[
    "bison_punch",
    //"bison_punch_npc", // remove damage reduction for npc bison-- they already got critical damage prevention
]
const float BISON_DAMAGE_REDUCTION_SEVERITY = 0.1
const float BISON_MELEE_DAMAGE_AGAINST_DOOMED_TITAN = 1.5 // bison can't termination, so we use higher doomed damage for them
const float BISON_SPEED_BOOST_SEVERITY = 0.0 // was 0.15, will cause bison's movement animations messed up
const float BISON_VICTIM_SLOW_SEVERITY = 0.0 // was 0.25, no slow effect for now
const float BISON_VICTIM_SLOW_DURATION = 0.0 // was 0.3, no slow effect for now
const float BISON_VICTIM_SLOW_FADETIME = 0.0 // was 1.0, no slow effect for now
const float BISON_DASH_RECHARGE_RATE_SCALE = 3.0 // since we've removed bison's speed boost, should give them more charge
const float BISON_DASH_RECHARGE_RATE_SCALE_TURBO = 1.75 // bison with pas_mobility_dash_capacity receive less rate
const float BISON_DASH_RECHARGE_DELAY_SCALE = 0.0 // bison won't have any delay recharging another dash 

// bison titan
const float BISON_GRAPPLE_ATTACHED_DURATION = 0.6 // grapple duration when attached a player or npc
const float BISON_GRAPPLE_COST_GRAPPLING_TITAN = 0.45
const float BISON_GRAPPLE_COST_GRAPPLING_PILOT = 0.25

const string BISON_PUNCH_MOD_NAME = "bison_punch"
const float TITAN_DASH_PUNCH_SPEEDSCALE_BISON = 1.25 // 1.27 when 10 fps, ~490 for ogre titans
const float TITAN_DASH_PUNCH_COOLDOWN_BISON = 2.0
const float HIT_CONFIRMATION_MULTIPLIER_AS_BISON = 0.2 // bison will have less screen shake

// due we've removed "pushback_scale" in melee_titan_punch_fighter.txt, needs to handle stuffs here
const float BISON_PUNCH_KNOCKBACK_SCALE = 0.5 // bit higher than "pushback_scale" "0.3"
const float BISON_PUNCH_SELF_KNOCKBACK_MULTIPLIER = 0.6 // still 0.3 to self( 0.5 * 0.6 )

// npc bison punch
// no any damage modify or disabling execution, just changes damageSourceId
const string NPC_BISON_PUNCH_MOD_NAME = "bison_punch_npc"

// moving grapple stuffs from sh_consts.gnut to here
const int BISON_GRAPPLE_DAMAGE_TITAN = 600 // vanilla value is 600
const float BISON_GRAPPLE_DAMAGE_DEBOUNCE_TIME = 0.6 // to avoid multi-attach damage spaming maybe... vanilla value is 1.0. current value matches BISON_GRAPPLE_ATTACHED_DURATION
const float BISON_GRAPPLE_SLOW_SEVERITY = 0.75 // vanilla value is 0.75
const float BISON_GRAPPLE_SLOW_DURATION = 0.6 // vanilla value is 0.9. current value matches BISON_GRAPPLE_ATTACHED_DURATION
const float BISON_GRAPPLE_SLOW_FADETIME = 0.5 // vanilla value is 0.5

struct
{
    table<entity, float> entLastTookBisonGrappleDamageTime
} file

void function BisonUtil_Init()
{
    // adding a new damageSourceId. it's gonna transfer to client automatically
	RegisterWeaponDamageSource( "melee_titan_punch_bison", "野牛重拳" )

    AddDamageFinalCallback( "npc_titan", OnBisonFinalDamage )
    AddDamageFinalCallback( "player", OnBisonFinalDamage )
    AddCallback_OnPilotBecomesTitan( OnPlayerBecomeBison )

    AddDamageCallbackSourceID( eDamageSourceId.melee_titan_punch_bison, BisonMelee_DamagedTarget )

    // bison grapple settings
	RegisterSignal( "BisonGrappled" )
    RegisterSignal( "BisonGrappleCancelled" )
    RegisterSignal( "BisonGrappleAttachedPlayerOrNPC" )
    RegisterSignal( "BisonGrappleAnimationFix" )
    AddCallback_MpAbilityGrapplePrimaryAttack( BisonOnUseGrapple )
    AddCallback_OnGrapple( OnBisonGrappleAttached )

    // melee, modified in sh_melee_titan.gnut
    TitanMelee_AddDashPunchSpeedScaleMod( BISON_PUNCH_MOD_NAME, TITAN_DASH_PUNCH_SPEEDSCALE_BISON ) // bison's dash is faster than normal ogre dash
    TitanMelee_AddDashPunchCooldownMod( BISON_PUNCH_MOD_NAME, TITAN_DASH_PUNCH_COOLDOWN_BISON ) // bison's dash has longer cooldown
    TitanMelee_AddCounterImmuneMod( BISON_PUNCH_MOD_NAME ) // bison's punch won't be countered
    TitanMelee_AddExecutionDisabledMod( BISON_PUNCH_MOD_NAME ) // execution is not allowed for bison punches
    TitanMelee_AddHitConfirmationMultiplierMod( BISON_PUNCH_MOD_NAME, HIT_CONFIRMATION_MULTIPLIER_AS_BISON ) // bison has less screen shake
    TitanMelee_AddDamageSourceIdMod( BISON_PUNCH_MOD_NAME, eDamageSourceId.melee_titan_punch_bison ) // bison unique melee damageSourceId

    // due we've removed "pushback_scale" in melee_titan_punch_fighter.txt, needs to handle stuffs here
    TitanMelee_AddKnockBackScaleMod( BISON_PUNCH_MOD_NAME, BISON_PUNCH_KNOCKBACK_SCALE )
    TitanMelee_AddSelfKnockBackMultiplierMod( BISON_PUNCH_MOD_NAME, BISON_PUNCH_SELF_KNOCKBACK_MULTIPLIER )

    // npc bison
    // no any damage modify or disabling execution, just changes damageSourceId
    // this only works for npcs spawned by extra_ai_spawner.gnut as we have a callback to change their display name
    TitanMelee_AddDamageSourceIdMod( NPC_BISON_PUNCH_MOD_NAME, eDamageSourceId.melee_titan_punch_bison )
}

// main checks
bool function TitanIsMeleeBison( entity titan )
{
    entity weapon = titan.GetMeleeWeapon()
    if ( !IsValid( weapon ) )
        return false

    array<string> meleeMods = weapon.GetMods()
    foreach ( mod in meleeMods )
    {
        if ( BISON_DAMAGE_REDUCTION_MELEE_MODS.contains( mod ) )
            return true
    }
    
    // no any valid mod
    return false
}

// STUB
bool function TitanIsGunnerBison( entity titan )
{
    return false
}

void function BisonMelee_DamagedTarget( entity victim, var damageInfo )
{
    // since we added fighter_melee, should check this...
    entity attacker = DamageInfo_GetAttacker( damageInfo )
    if ( !IsValid( attacker ) )
        return
    if ( !TitanIsMeleeBison( attacker ) )
        return
    //print( "bison damaged target!" ) // debugging for explosion damages
    //print( "damage value: " + string( DamageInfo_GetDamage( damageInfo ) ) )
    

    if( !victim.IsTitan() )
        return
    entity soul = victim.GetTitanSoul()
    if( !IsValid( soul ) )
        return

    // titan slow effect needs to apply on soul
    StatusEffect_AddTimed( soul, eStatusEffect.move_slow, BISON_VICTIM_SLOW_SEVERITY, BISON_VICTIM_SLOW_DURATION, BISON_VICTIM_SLOW_FADETIME )
    StatusEffect_AddTimed( soul, eStatusEffect.dodge_speed_slow, BISON_VICTIM_SLOW_SEVERITY, BISON_VICTIM_SLOW_DURATION, BISON_VICTIM_SLOW_FADETIME ) 
    // do bonus damage as we won't execute our target
    if( soul.IsDoomed() )
        DamageInfo_ScaleDamage( damageInfo, BISON_MELEE_DAMAGE_AGAINST_DOOMED_TITAN )
}

void function OnBisonFinalDamage( entity victim, var damageInfo )
{
    if ( !victim.IsTitan() )
        return
    
    if ( TitanIsMeleeBison( victim ) )
        DamageInfo_ScaleDamage( damageInfo, 1.0 - BISON_DAMAGE_REDUCTION_SEVERITY )
}

void function OnPlayerBecomeBison( entity player, entity titan )
{
    HandlePlayerMeleeBisonAbility( player )
}

void function HandlePlayerMeleeBisonAbility( entity player )
{
    thread HandlePlayerMeleeBisonAbility_Threaded( player )
}

void function HandlePlayerMeleeBisonAbility_Threaded( entity player )
{
    wait 0.1
    if( !IsValid( player ) )
        return
    entity soul = player.GetTitanSoul()
    if( !IsValid( soul ) )
        return
    entity meleeWeapon = player.GetMeleeWeapon()
    if( !IsValid( meleeWeapon ) )
        return
    if( !meleeWeapon.HasMod( "bison_punch" ) )
        return

    // can't handle offhand cancel
    // best method is through removing weapon's pullout time in settings file
    //thread BisonForcedOffhandCancelThink( player )

    soul.EndSignal( "OnDestroy" )
    player.EndSignal( "OnDestroy" )
	player.EndSignal( "OnDeath" )
	player.EndSignal( "DisembarkingTitan" )
	player.EndSignal( "TitanEjectionStarted" )
    
    // for spawner using player.GiveExtraWeaponMod( "amped_tacticals" ), force remove it
    /* // now handle by using non-conflict weapon settings
    entity grappleWeapon
    foreach( entity offhand in player.GetOffhandWeapons() )
    {
        if( offhand.GetWeaponClassName() == "mp_ability_grapple" && offhand.HasMod( "bison_grapple" ) )
            grappleWeapon = offhand
    }
    if( IsValid( grappleWeapon ) )
    {
        array<string> mods = grappleWeapon.GetMods()
        mods.removebyvalue( "amped_tacticals" )
        mods.removebyvalue( "all_grapple" )
        print( mods )
        grappleWeapon.SetMods( mods )
    }
    */

    //Bison have damage reduction
	soul.SetPreventCrits( true )
    // speed
    int speedBoost = StatusEffect_AddEndless( player, eStatusEffect.speed_boost, BISON_SPEED_BOOST_SEVERITY )
    
    // dash power regen
    array<string> classMods = player.GetPlayerSettingsMods()
    float powerRegenScale = BISON_DASH_RECHARGE_RATE_SCALE
    if ( classMods.contains( "pas_mobility_dash_capacity" ) || classMods.contains( "turbo_titan" ) ) // has turbo engine or in turbo titan mode
        powerRegenScale = BISON_DASH_RECHARGE_RATE_SCALE_TURBO // nerfed rate
    player.SetPowerRegenRateScale( powerRegenScale )
    player.SetDodgePowerDelayScale( BISON_DASH_RECHARGE_DELAY_SCALE )

    OnThreadEnd(
        function(): ( player, soul, speedBoost )
        {
            if( IsValid( player ) )
            {
                // clean up
                StatusEffect_Stop( player, speedBoost )
                player.SetPowerRegenRateScale( 1.0 )
                player.SetDodgePowerDelayScale( 1.0 )
            }
        }
    )

    WaitForever()
}

// can't handle offhand cancel
// best method is through removing weapon's pullout time in settings file
/*
void function BisonForcedOffhandCancelThink( entity player )
{
    player.EndSignal( "OnDestroy" )
	player.EndSignal( "OnDeath" )
	player.EndSignal( "DisembarkingTitan" )
	player.EndSignal( "TitanEjectionStarted" )

    while ( true )
    {
        WaitFrame()
        entity activeWeapon = player.GetActiveWeapon()
        entity meleeWeapon = player.GetMeleeWeapon()
        if ( !IsValid( activeWeapon ) || !IsValid( meleeWeapon ) )
            continue
        if ( activeWeapon == meleeWeapon )
            continue

        if ( activeWeapon.IsWeaponOffhand() && activeWeapon.IsChargeWeapon() && player.IsInputCommandHeld( IN_MELEE ) )
        {
            player.ClearOffhand()
            //player.SetActiveWeaponByName( meleeWeapon.GetWeaponClassName() )
        }
    }
}
*/

void function BisonOnUseGrapple( entity weapon, WeaponPrimaryAttackParams attackParams )
{
    // for bison: not allowed to control players or npcs forever by grapple
	if( weapon.HasMod( "bison_grapple" ) )
	{
        entity player = weapon.GetWeaponOwner()
		thread BisonDelayedAutoDetach( player )
        // try to fix grapple pull animation by any means...
        // won't work at all...
        //thread ScriptedBisonGrappleAnimationFix( player )
	}
}

void function BisonDelayedAutoDetach( entity player )
{
    player.EndSignal( "OnDestroy" )
    player.EndSignal( "OnDeath" )
	player.Signal( "BisonGrappled" )
	player.EndSignal( "BisonGrappled" )
    player.EndSignal( "BisonGrappleCancelled" )

    // check cancelling through crouching
	AddButtonPressedPlayerInputCallback( player, IN_DUCK, PlayerPressedGrappleDetachButton )
	AddButtonPressedPlayerInputCallback( player, IN_DUCKTOGGLE, PlayerPressedGrappleDetachButton )

    OnThreadEnd
    (
        function(): ( player )
        {
            if ( IsValid( player ) )
            {
                // clean up
				RemoveButtonPressedPlayerInputCallback( player, IN_DUCK, PlayerPressedGrappleDetachButton )
				RemoveButtonPressedPlayerInputCallback( player, IN_DUCKTOGGLE, PlayerPressedGrappleDetachButton )
                player.Signal( "BisonGrappleCancelled" ) // new adding, to end animation fixes
            }
        }
    )

	player.WaitSignal( "BisonGrappleAttachedPlayerOrNPC" )
    wait BISON_GRAPPLE_ATTACHED_DURATION // limited grapple duration
	if( IsAlive( player ) )
		player.Grapple( < 0,0,0 > ) // grapple again while another one is active will cancel it
}

void function PlayerPressedGrappleDetachButton( entity player )
{
	player.Signal( "BisonGrappleCancelled" )
}

void function OnBisonGrappleAttached( entity player, entity hitEnt, vector hitpos, vector hitNormal )
{
    // anti-crash
    if ( !IsValid( hitEnt ) )
        return
    if ( !IsAlive( hitEnt ) )
        return

    entity grappleWeapon = null
	foreach( entity offhand in player.GetOffhandWeapons() )
	{
		if( offhand.GetWeaponClassName() == "mp_ability_grapple" )
			grappleWeapon = offhand
	}

    if ( !IsValid( grappleWeapon ) )
        return

    if ( !grappleWeapon.HasMod( "bison_grapple" ) )
        return

    // fix again, with reduced max duration
    // won't work at all...
    //thread ScriptedBisonGrappleAnimationFix( player, BISON_GRAPPLE_ATTACHED_DURATION )
    
    // moving damage stuffs from _codecallbacks.gnut, allow npcs to be damaged by bison
    if ( hitEnt.IsNPC() || hitEnt.IsPlayer() )
    {
        if ( hitEnt.GetTeam() != player.GetTeam() || FriendlyFire_IsEnabled() )
        {
            if ( !( hitEnt in file.entLastTookBisonGrappleDamageTime ) )
                file.entLastTookBisonGrappleDamageTime[ hitEnt ] <- 0.0
            
            // 1s damage protection...
            if ( file.entLastTookBisonGrappleDamageTime[ hitEnt ] + BISON_GRAPPLE_DAMAGE_DEBOUNCE_TIME < Time() )
            {
                if ( player.IsTitan() )
                {
                    // now we only do damage to titan victim
                    if ( hitEnt.IsTitan() )
                        hitEnt.TakeDamage( BISON_GRAPPLE_DAMAGE_TITAN, player, player, { origin = hitpos, scriptType = DF_GIB, damageSourceId = eDamageSourceId.titan_grapple } )

                    // idk why there is also slow, just leaving here
                    // also adding move_slow for pilot victim
                    entity slowEnt = hitEnt
                    if ( hitEnt.IsTitan() && IsValid( hitEnt.GetTitanSoul() ) )
                        slowEnt = hitEnt.GetTitanSoul()

                    StatusEffect_AddTimed( slowEnt, eStatusEffect.move_slow, BISON_GRAPPLE_SLOW_SEVERITY, BISON_GRAPPLE_SLOW_DURATION + BISON_GRAPPLE_SLOW_FADETIME, BISON_GRAPPLE_SLOW_FADETIME )
                    if ( hitEnt.IsTitan() )
                        StatusEffect_AddTimed( slowEnt, eStatusEffect.dodge_speed_slow, BISON_GRAPPLE_SLOW_SEVERITY, BISON_GRAPPLE_SLOW_DURATION + BISON_GRAPPLE_SLOW_FADETIME, BISON_GRAPPLE_SLOW_FADETIME )
                }

                // this should only be updated when target actually takes damage
                file.entLastTookBisonGrappleDamageTime[ hitEnt ] = Time()
            }
        }
    }

    float powerPerGrapple = grappleWeapon.GetWeaponSettingFloat( eWeaponVar.grapple_power_required )
    float ownerGrapplePower = player.GetSuitGrapplePower()
    int grappleShotsLeft = int ( ownerGrapplePower / powerPerGrapple )
    float grapplePowerCostMax
    if ( ownerGrapplePower == 100 ) // max power
        grapplePowerCostMax = powerPerGrapple // max cost
    else
        grapplePowerCostMax = ownerGrapplePower - ( grappleShotsLeft * powerPerGrapple ) // will never use next grapple's power

    // npc grapple cost
    if ( hitEnt.IsNPC() )
    {
        player.Signal( "BisonGrappleAttachedPlayerOrNPC" ) // tell BisonDelayedAutoDetach() we've attached a movable target, start detach countdown
        if ( hitEnt.IsTitan() )
        {
            float powerReduction = min ( grapplePowerCostMax, BISON_GRAPPLE_COST_GRAPPLING_TITAN )
            player.SetSuitGrapplePower( max ( 0, ownerGrapplePower - powerReduction ) )
        }
    }
    // player grapple costs
    if ( hitEnt.IsPlayer() )
    {
        player.Signal( "BisonGrappleAttachedPlayerOrNPC" ) // tell BisonDelayedAutoDetach() we've attached a movable target, start detach countdown
        if ( hitEnt.IsTitan() )
        {
            float powerReduction = min ( grapplePowerCostMax, BISON_GRAPPLE_COST_GRAPPLING_TITAN )
            player.SetSuitGrapplePower( max ( 0, ownerGrapplePower - powerReduction ) )
        }
        else
        {
            float powerReduction = min ( grapplePowerCostMax, BISON_GRAPPLE_COST_GRAPPLING_PILOT )
            player.SetSuitGrapplePower( max ( 0, ownerGrapplePower - powerReduction ) )
        }
    }
}

void function ScriptedBisonGrappleAnimationFix( entity player, float maxDuration = 3.0 )
{
    player.EndSignal( "OnDestroy" )
    player.EndSignal( "OnDeath" )
    player.Signal( "BisonGrappleAnimationFix" )
    player.EndSignal( "BisonGrappleAnimationFix" )
    // many ways to stop fix so that we don't get weird behavior
	player.EndSignal( "BisonGrappled" )
    player.EndSignal( "BisonGrappleCancelled" )
    player.EndSignal( "BisonGrappleAttachedPlayerOrNPC" )

    float endTime = Time() + maxDuration // max duration... maybe no need? should be handled by "BisonGrappleCancelled" signal, or we just return
    while ( Time() < endTime )
    {
        WaitFrame() // idk we use higher framerate frequently causes performance issue or not... just feels better
        // don't overwrite offhand / melee attack animation
        entity activeWeapon = player.GetActiveWeapon()
        bool activeWeaponIsNonMeleeOffhand
        if ( IsValid( activeWeapon ) && activeWeapon.IsWeaponOffhand() && activeWeapon != player.GetMeleeWeapon() )
            activeWeaponIsNonMeleeOffhand = true
        if ( player.PlayerMelee_IsAttackActive() || !IsValid( activeWeapon ) || activeWeaponIsNonMeleeOffhand )
        {
            //print( "animation fix ignored!" )
            continue
        }
        //print( "fixing animation!" )
        // won't work at all...
        player.Anim_StopGesture( DEFAULT_SCRIPTED_ANIMATION_BLEND_TIME ) // force stop weird death animation
    }
}