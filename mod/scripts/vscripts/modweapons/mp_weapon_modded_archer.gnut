untyped

global function MpWeaponModdedArcher_Init

// mainly shared utility with mp_weapon_rocket_launcher.nut
global function ModdedArcher_WeaponHasMod
global function ModdedArcher_ProjectileHasMod
//

global function OnWeaponActivate_weapon_modded_archer
global function OnWeaponDeactivate_weapon_modded_archer
global function OnWeaponPrimaryAttack_weapon_modded_archer
global function OnWeaponOwnerChanged_weapon_modded_archer

global function OnProjectileCollision_weapon_modded_archer

global function OnWeaponStartZoomIn_weapon_modded_archer
global function OnWeaponStartZoomOut_weapon_modded_archer

#if SERVER
global function OnWeaponNpcPrimaryAttack_weapon_modded_archer
#endif // #if SERVER

// modded rocket
const array<string> MODDED_ARCHER_MODS = // we run this script if archer has these mods
[
	"no_lock_required",
	"guided_missile",
	"cruise_missile",
]

// in this file all archers should have ability to direct fire
/*
const array<string> NO_LOCK_REQUIRED_MODS = // we should fix visual for these mods
[
	"no_lock_required",
	"guided_missile",
]
*/

// modifiable consts
const float GUIDED_MISSILE_LIFETIME = 20 // guided missile lifetiime
const float CRUISE_MISSILE_LIFETIME = 99999 // infinite lifetime cause I don't want make missile early detonate

struct
{
	// visual fix
	table<entity, entity> rocketLaserTable // save existing laser effect

	table<entity, entity> playerActiveCruiseMissile
} file

void function MpWeaponModdedArcher_Init()
{
	RegisterSignal( "StopGuidedLaser" )

	#if SERVER
		RegisterSignal( "CalculateCruiseMissilePoint" )
		RegisterSignal( "MissileImpact" )

		AddCallback_OnClientConnected( OnClientConnected )
	#endif
}

// mainly shared utility with mp_weapon_modded_archer.nut
bool function ModdedArcher_WeaponHasMod( entity weapon )
{
	foreach ( string mod in MODDED_ARCHER_MODS )
	{
		if ( weapon.HasMod( mod ) )
			return true
	}

	return false
}

bool function ModdedArcher_ProjectileHasMod( entity projectile )
{
	array<string> mods = Vortex_GetRefiredProjectileMods( projectile ) // modded weapon refire behavior
	foreach ( string mod in MODDED_ARCHER_MODS )
	{
		if ( mods.contains( mod ) )
			return true
	}

	return false
}
//

void function OnClientConnected( entity player )
{
	file.playerActiveCruiseMissile[ player ] <- null
}

function MissileThink( weapon, missile )
{
	expect entity( missile )

	#if SERVER
		missile.EndSignal( "OnDestroy" )

		bool playedWarning = false

		while ( IsValid( missile ) )
		{
			entity target = missile.GetMissileTarget()

			if ( IsValid( target ) && target.IsPlayer() )
			{
				float distance = Distance( missile.GetOrigin(), target.GetOrigin() )

				if ( distance < 1536 && !playedWarning )
				{
					EmitSoundOnEntityOnlyToPlayer( target, target, "titan_cockpit_missile_close_warning" )
					playedWarning = true
				}
			}

			WaitFrame()
		}
	#endif
}

void function OnWeaponActivate_weapon_modded_archer( entity weapon )
{
	if ( !( "initialized" in weapon.s ) )
	{
		weapon.s.missileThinkThread <- MissileThink
		weapon.s.initialized <- true
	}

	bool hasGuidedMissiles = weapon.HasMod( "guided_missile" )

	if ( !hasGuidedMissiles )
	{
		// modded weapon
		// in this file all archers should have ability to direct fire
		//if( weapon.HasMod("no_lock_required") )
			SmartAmmo_SetAllowUnlockedFiring( weapon, true )
		//else // vanilla behavior
		//	SmartAmmo_SetAllowUnlockedFiring( weapon, false )
		
		SmartAmmo_SetMissileSpeed( weapon, 1200 )
		SmartAmmo_SetMissileHomingSpeed( weapon, 125 )

		if ( weapon.HasMod( "burn_mod_rocket_launcher" ) )
			SmartAmmo_SetMissileSpeedLimit( weapon, 1300 )
		else
			SmartAmmo_SetMissileSpeedLimit( weapon, 1400 )

		SmartAmmo_SetMissileShouldDropKick( weapon, false )  // TODO set to true to see drop kick behavior issues
		SmartAmmo_SetUnlockAfterBurst( weapon, true )
	}

	entity weaponOwner = weapon.GetWeaponOwner()

	if ( hasGuidedMissiles )
	{
		if ( !("guidedLaserPoint" in weaponOwner.s) )
			weaponOwner.s.guidedLaserPoint <- null
		
		thread CalculateGuidancePoint( weapon, weaponOwner )
	}

	if ( weapon.HasMod( "cruise_missile" ) )
	{
		#if SERVER
			thread CalculateCruiseMissilePoint( weapon, weaponOwner )
		#endif
	}

	// modified to add rocket lasers
#if SERVER
	//if ( weaponOwner.IsNPC() )
	//	thread DelayedRocketLaserStart( weapon, weaponOwner )
#endif
}

void function OnWeaponDeactivate_weapon_modded_archer( entity weapon )
{
	if ( weapon.HasMod( "guided_missile" ) )
	{
		weapon.Signal( "StopGuidedLaser" )
	}

	// modified to add rocket lasers
	entity weaponOwner = weapon.GetWeaponOwner()
#if SERVER
	if ( weaponOwner.IsNPC() )
		weapon.StopWeaponEffect( $"P_wpn_lasercannon_aim", $"P_wpn_lasercannon_aim" )
	
	// defensive fix
	ADSLaserEnd( weapon )
#endif
}

var function OnWeaponPrimaryAttack_weapon_modded_archer( entity weapon, WeaponPrimaryAttackParams attackParams )
{
	entity weaponOwner = weapon.GetWeaponOwner()

	float zoomFrac = weaponOwner.GetZoomFrac()
	if ( zoomFrac < 1 )
		return 0

	vector angles = VectorToAngles( weaponOwner.GetViewVector() )
	vector right = AnglesToRight( angles )
	vector up = AnglesToUp( angles )
	#if SERVER
		if ( weaponOwner.GetTitanSoulBeingRodeoed() != null )
			attackParams.pos = attackParams.pos + up * 20
	#endif

	if ( weapon.HasMod( "guided_missile" ) ) // guided missile variant
	{
		if( !weapon.IsWeaponAdsButtonPressed() )
			return 0

		bool shouldPredict = weapon.ShouldPredictProjectiles()
		#if CLIENT
			if ( !shouldPredict )
				return 1
		#endif

		float speed = 1200.0
		if ( weapon.HasMod("titanhammer") )
			speed = 800.0

		weapon.EmitWeaponNpcSound( LOUD_WEAPON_AI_SOUND_RADIUS_MP, 0.2 )

		entity missile = weapon.FireWeaponMissile( attackParams.pos, attackParams.dir, speed, damageTypes.projectileImpact | DF_IMPACT, damageTypes.explosive, false, shouldPredict )

		if ( missile )
		{
			if( "guidedMissileTarget" in weapon.s && IsValid( weapon.s.guidedMissileTarget ) )
			{
				missile.SetMissileTarget( weapon.s.guidedMissileTarget, Vector( 0, 0, 0 ) )
				missile.SetHomingSpeeds( 300, 0 )
			}

			InitializeGuidedMissile( weaponOwner, missile )
			// modified here
			#if SERVER
				RocketEffectFix( weapon ) // fix effect
				thread GuidedMissileReloadThink( weapon, weaponOwner, missile ) // for better missile controling?
			#endif
		}
	}
	else if ( weapon.HasMod( "cruise_missile" ) ) // COD-like cruise missile
	{
		//if ( IsValid( file.playerActiveCruiseMissile[ weaponOwner ] ) )
		//	return 0

		bool shouldPredict = weapon.ShouldPredictProjectiles()
		#if CLIENT
			if ( !shouldPredict )
				return 1
		#endif

		float speed = 800.0

		entity missile = weapon.FireWeaponMissile( attackParams.pos, attackParams.dir, speed, damageTypes.projectileImpact | DF_IMPACT, damageTypes.explosive, false, shouldPredict )

		if ( missile )
		{
			#if SERVER
				RocketEffectFix( weapon, true ) // emit sound only
				thread CruiseMissileThink( weapon, weaponOwner, missile )
			#endif
		}
	}
	else // basic firing
	{
		int fired = SmartAmmo_FireWeapon( weapon, attackParams, damageTypes.projectileImpact, damageTypes.explosive )

		if ( fired )
		{
			weapon.EmitWeaponNpcSound( LOUD_WEAPON_AI_SOUND_RADIUS_MP, 0.2 )

			// no lock required version
			#if SERVER
				// in this file all archers should have ability to direct fire
				//if ( weapon.HasMod( "no_lock_required" ) )
					RocketEffectFix( weapon )
			#endif
		}

		return fired
	}
}

void function OnProjectileCollision_weapon_modded_archer( entity projectile, vector pos, vector normal, entity hitEnt, int hitbox, bool isCritical )
{
	array<string> refiredMods = Vortex_GetRefiredProjectileMods( projectile )
	// direct hit
	if ( refiredMods.contains( "direct_hit" ) )
        OnProjectileCollision_DirectHit( projectile, pos, normal, hitEnt, hitbox, isCritical )

#if SERVER
	// needed for cruise missile to get signal before missile gets destroyed
	projectile.Signal( "MissileImpact" )

	// visual fix checks, no need to handle refiring cause refired projectile already unpredicted
	array<string> mods = projectile.ProjectileGetMods()
	// in this file all archers should have ability to direct fire
	/*
	bool shouldFixVisual = false
	foreach ( string mod in NO_LOCK_REQUIRED_MODS )
	{
		if ( mods.contains( mod ) )
		{
			shouldFixVisual = true
			break
		}
	}
	//print( "shouldFixVisual: " + string( shouldFixVisual ) )
	*/
	bool shouldFixVisual = true // always do fix!
	if ( shouldFixVisual )
	{
		// do a fake explosion effect for better client visual
		FixImpactEffectForProjectileAtPosition( projectile, pos ) // shared from _unpredicted_impact_fix.gnut
	}
#endif
}

#if SERVER
var function OnWeaponNpcPrimaryAttack_weapon_modded_archer( entity weapon, WeaponPrimaryAttackParams attackParams )
{
	// NPC can shoot the weapon at non-players, but when shooting at players it must be a titan
	// nessie: add back this if we need
	if ( weapon.HasMod( "npc_disable_fire_at_pilot" ) ) // now specified for this mod
	{
		entity owner = weapon.GetWeaponOwner()
		if ( IsValid( owner ) )
		{
			entity enemy = owner.GetEnemy()
			if ( IsValid( enemy ) )
			{
				if ( enemy.IsPlayer() && !enemy.IsTitan() )
					return 0
			}
		}
	}

	weapon.EmitWeaponNpcSound( LOUD_WEAPON_AI_SOUND_RADIUS_MP, 0.2 )
	entity missile = weapon.FireWeaponMissile( attackParams.pos, attackParams.dir, 1800.0, damageTypes.projectileImpact, damageTypes.explosive, false, PROJECTILE_NOT_PREDICTED )
	if ( missile )
	{
		missile.InitMissileForRandomDriftFromWeaponSettings( attackParams.pos, attackParams.dir )
		if ( weapon.w.missileFiredCallback != null )
		{
			weapon.w.missileFiredCallback( missile, weapon.GetWeaponOwner() )
		}
	}
}
#endif // #if SERVER

//GUIDED MISSILE FUNCTIONS
function CalculateGuidancePoint( entity weapon, entity weaponOwner )
{
	weaponOwner.EndSignal( "OnDestroy" )
	weapon.EndSignal( "OnDestroy" )
	weapon.EndSignal( "StopGuidedLaser" )

	entity info_target
	#if SERVER
		info_target = CreateEntity( "info_target" )
		info_target.SetOrigin( weapon.GetOrigin() )
		info_target.SetInvulnerable()
		DispatchSpawn( info_target )
		weapon.s.guidedMissileTarget <- info_target
	#endif

	OnThreadEnd(
		function() : ( weapon, info_target )
		{
			if ( IsValid( info_target ) )
			{
				info_target.Kill_Deprecated_UseDestroyInstead()
				if ( IsValid( weapon ) ) // defensive fix
					delete weapon.s.guidedMissileTarget
			}
		}
	)

	while ( true )
	{
		if ( !IsValid_ThisFrame( weaponOwner ) || !IsValid_ThisFrame( weapon ) )
			return

		weaponOwner.s.guidedLaserPoint = null
		if ( weapon.IsWeaponInAds())
		{
			TraceResults result = GetViewTrace( weaponOwner )
			weaponOwner.s.guidedLaserPoint = result.endPos
			#if SERVER
				info_target.SetOrigin( result.endPos )
			#endif
		}

		WaitFrame()
	}
}

function InitializeGuidedMissile( entity weaponOwner, entity missile )
{
		missile.s.guidedMissile <- true
		if ( "missileInFlight" in weaponOwner.s )
			weaponOwner.s.missileInFlight = true
		else
			weaponOwner.s.missileInFlight <- true

		missile.kv.lifetime = GUIDED_MISSILE_LIFETIME

		#if SERVER
			missile.SetOwner( weaponOwner )
			thread playerHasMissileInFlight( weaponOwner, missile )
		#endif
}

#if SERVER
function playerHasMissileInFlight( entity weaponOwner, entity missile )
{
	weaponOwner.EndSignal( "OnDestroy" )

	OnThreadEnd(
		function() : ( weaponOwner )
		{
			if ( IsValid( weaponOwner ) )
			{
				weaponOwner.s.missileInFlight = false
				//Using a remote call because if this thread is on the client it gets triggered prematurely due to prediction.
				Remote_CallFunction_NonReplay( weaponOwner, "ServerCallback_GuidedMissileDestroyed" )
			}
		}
	)

	WaitSignal( missile, "OnDestroy" )
}
#endif // SERVER


void function OnWeaponOwnerChanged_weapon_modded_archer( entity weapon, WeaponOwnerChangedParams changeParams )
{
	#if SERVER
		weapon.w.missileFiredCallback = null
	#endif
}


// modded callbacks
void function OnWeaponStartZoomIn_weapon_modded_archer( entity weapon )
{
	if( !weapon.HasMod( "guided_missile" ) )
		return
#if SERVER
	ADSLaserStart( weapon )
#endif
}

void function OnWeaponStartZoomOut_weapon_modded_archer( entity weapon )
{
	if( !weapon.HasMod( "guided_missile" ) )
		return
	entity weaponOwner = weapon.GetWeaponOwner()
#if SERVER
	ADSLaserEnd( weapon )
#endif
}

// modded functions
#if SERVER
void function ADSLaserStart( entity weapon )
{
	thread ADSLaserStart_Threaded( weapon )
}

void function ADSLaserStart_Threaded( entity weapon )
{
	ADSLaserEnd( weapon ) // defensive fix: stop existing laser
	
	entity owner = weapon.GetWeaponOwner()
	if ( !IsValid( owner ) )
		return
	if ( !owner.IsPlayer() )
		return

	WaitFrame()
	if( !IsAlive( owner ) || !IsValid( weapon ) )
		return
	if( owner.GetActiveWeapon() != weapon )
		return

	entity viewModelEnt = owner.GetViewModelEntity()
	if ( IsValid( viewModelEnt ) && EntHasModelSet( viewModelEnt ) )
	{
		if ( !( viewModelEnt in file.rocketLaserTable ) )
			file.rocketLaserTable[ viewModelEnt ] <- null
		if ( IsValid( file.rocketLaserTable[ viewModelEnt ] ) ) // already has a laser valid
			return
		// get attachment validation
		int attachID = viewModelEnt.LookupAttachment( "flashlight" )
		if ( attachID <= 0 )
			return
		// make non_predicted client have proper fx
		entity fx = PlayLoopFXOnEntity( $"P_wpn_lasercannon_aim", viewModelEnt, "flashlight" )
		fx.SetStopType( "DestroyImmediately" ) // ensure this fx gets destroyed immediately
		file.rocketLaserTable[ viewModelEnt ] = fx
	}
	// thirdperson fx
	weapon.PlayWeaponEffect( $"", $"P_wpn_lasercannon_aim", "flashlight" )
}

void function ADSLaserEnd( entity weapon )
{
	entity owner = weapon.GetWeaponOwner()
	if ( !IsValid( owner ) )
		return
	if ( !owner.IsPlayer() )
		return

	entity viewModelEnt = owner.GetViewModelEntity()
	if ( IsValid( viewModelEnt ) )
	{
		entity laserFX
		if ( viewModelEnt in file.rocketLaserTable )
			laserFX = file.rocketLaserTable[ viewModelEnt ]
		if ( IsValid( laserFX ) )
		{
			EntFireByHandle( laserFX, "Stop", "", 0, null, null )
			laserFX.Destroy()
		}
	}
	// thirdperson fx
	weapon.StopWeaponEffect( $"", $"P_wpn_lasercannon_aim" )
}

// modified function
void function GuidedMissileReloadThink( entity weapon, entity weaponOwner, entity missile )
{
	if( !weaponOwner.IsPlayer() )
		return
	weapon.EndSignal( "OnDestroy" )
	weaponOwner.EndSignal( "OnDeath" )
	weaponOwner.EndSignal( "OnDestroy" )
	missile.EndSignal( "OnDestroy" ) // wait for missile explode

	table fireInterval = {}
	fireInterval.timeLeft <- 0.0

	OnThreadEnd
	(
		function():( weapon, weaponOwner, fireInterval )
		{
			//print( "guiding end!" )
			if( IsValid( weapon ) )
			{
				thread DelayedEnableRocketAttack( weapon, expect float ( fireInterval.timeLeft ) )
				//weapon.RemoveMod( "disable_reload" ) // client can't predict this
			}
		}
	)
	
	// try to prevent SetNextAttackAllowedTime() during a firing interval( which will make it not work )
	float minReloadDelay = 1 / weapon.GetWeaponSettingFloat( eWeaponVar.fire_rate )
	fireInterval.timeLeft = minReloadDelay
	float minReloadTime = Time() + minReloadDelay
	weapon.AddMod( "disable_reload" ) // client can't predict this and it's not necessary, just to prevent softlock
	while( true )
	{
		if ( Time() >= minReloadTime ) // must in valid reload time to trigger manually reloading
		{
			fireInterval.timeLeft = 0.0
			if( weaponOwner.IsInputCommandHeld( IN_RELOAD ) || weaponOwner.IsInputCommandHeld( IN_USE_AND_RELOAD ) )
				break
			if( weaponOwner.GetActiveWeapon() != weapon )
				break
			if( !weapon.IsWeaponAdsButtonPressed() )
				break
		}
		else // still in interval
			fireInterval.timeLeft = minReloadTime - Time()
		WaitFrame()
	}
	//print( "guiding interrupted!" )
}

const float GUIDED_MISSILE_RELOAD_INTERVAL = 0.3
void function DelayedEnableRocketAttack( entity weapon, float delay )
{
	weapon.EndSignal( "OnDestroy" )
	wait delay + GUIDED_MISSILE_RELOAD_INTERVAL
	// refresh next attack time, so the weapon will start reloading. add 0.3s more for defensive fix
	weapon.SetNextAttackAllowedTime( Time() + GUIDED_MISSILE_RELOAD_INTERVAL )
	//weapon.SetWeaponPrimaryClipCount( 0 )
	// try to prevent SetNextAttackAllowedTime() while reloading, which will break ammo system
	wait GUIDED_MISSILE_RELOAD_INTERVAL + 0.2
	weapon.RemoveMod( "disable_reload" )
}

// Rocket Visual Effects
void function RocketEffectFix( entity weapon, bool doSoundOnly = false )
{
	entity owner = weapon.GetWeaponOwner()
	if ( !IsValid( owner ) )
		return
	if ( !owner.IsPlayer() )
		return

	if( IsAlive( owner ) && IsValid( weapon ) )
	{
		// play a sound, hardcoded
		EmitSoundOnEntityOnlyToPlayer( weapon, owner, "Weapon_Archer_Fire_1P" )

		// do sound only
		if ( doSoundOnly )
			return

		entity viewModelEnt = owner.GetViewModelEntity()
		// firstperson fx, force play it on vm
		if ( IsValid( viewModelEnt ) )
			thread RocketMuzzleThink( weapon, owner )
	}
}

const float MUZZLE_MAX_DURATION = 2.0 // assume this is the fx's duration
void function RocketMuzzleThink( entity weapon, entity owner )
{
	entity viewModelEnt = owner.GetViewModelEntity()
	if ( !IsValid( viewModelEnt ) || !EntHasModelSet( viewModelEnt ) )
		return
	// get attachment validation
	int attachID = viewModelEnt.LookupAttachment( "muzzle_flash" )
	if ( attachID <= 0 )
		return

	// firstperson fx, force play it on vm
	asset muzzleFX = weapon.GetWeaponSettingAsset( eWeaponVar.fx_muzzle_flash_view )
	entity fx = PlayFXOnEntity( muzzleFX, viewModelEnt, "muzzle_flash" )
	fx.SetStopType( "DestroyImmediately" ) // ensure this fx gets destroyed immediately
	viewModelEnt.EndSignal( "OnDestroy" )
	owner.EndSignal( "OnDestroy" )
	fx.EndSignal( "OnDestroy" )

	OnThreadEnd
	(
		function(): ( fx )
		{
			if ( IsValid( fx ) )
			{
				//print( "try to stop muzzle fx" )
				EffectStop( fx )
			}
			//else
			//	print( "fx destroyed" )
		}
	)

	float endTime = Time() + MUZZLE_MAX_DURATION
	while ( Time() < endTime )
	{
		entity activeWeapon = owner.GetActiveWeapon()
		if ( !IsValid( activeWeapon ) )
			break
		if ( activeWeapon != weapon ) // player switched weapon...
		{
			//print( "player switched weapon!" )
			break // try to stop fx
		}
		WaitFrame()
	}
}

void function DelayedRocketLaserStart( entity weapon, entity weaponOwner )
{
	if ( IsValid( weapon ) && IsValid( weaponOwner ) && weapon == weaponOwner.GetActiveWeapon() )
	{
		weapon.PlayWeaponEffect( $"P_wpn_lasercannon_aim", $"P_wpn_lasercannon_aim", "flashlight" )
	}
}

// COD-Like Cruise Missile
void function CruiseMissileThink( entity weapon, entity weaponOwner, entity missile )
{
	missile.EndSignal( "OnDestroy" )

	missile.SetModel( $"models/weapons/bullets/projectile_rocket_large.mdl" ) // $"models/weapons/bullets/projectile_rocket_largest.mdl"
	if ( missile.IsMarkedForDeletion() ) // may happen after changing model
		return

	weaponOwner.EndSignal( "OnDestroy" )
	weaponOwner.EndSignal( "OnDeath" )

	if( "guidedMissileTarget" in weapon.s && IsValid( weapon.s.guidedMissileTarget ) )
	{
		missile.SetMissileTarget( weapon.s.guidedMissileTarget, Vector( 0, 0, 0 ) )
		missile.SetHomingSpeeds( 500, 0 )
	}

	missile.kv.lifetime = CRUISE_MISSILE_LIFETIME
	file.playerActiveCruiseMissile[ weaponOwner ] = missile

	// now try to use viewcontrol entity
	//CruiseMissilePlayerTrackView( weaponOwner, missile )

	HolsterAndDisableWeapons( weaponOwner )
	//weapon.AddMod( "disable_reload" ) // we'll change player's view, desync won't influence too much
	
	// also not working well
	entity camera = CreateEntity( "point_viewcontrol" )
	/*
	camera.SetParent( missile, "exhaust" )
	camera.SetAngles( < 180, 0, -90 > )
	//camera.SetOrigin( < -100, 0, 80 > )
	camera.kv.spawnflags = 56
	DispatchSpawn( camera )

	weaponOwner.SetViewEntity( camera, true )
	*/

	table results = {
		firingStartAngles = weaponOwner.EyeAngles()
	}

	//HACK: using turret
	entity turret = CreateTurretEnt( missile.GetOrigin(), missile.GetAngles(), null, ROCKET_TURRET_MODEL, "PROTO_at_turret" )
	turret.Hide()
	//turret.NotSolid()
	turret.SetInvulnerable()
	turret.SetParent( missile, "exhaust" )
	turret.SetAngles( < 180, 0, -90 > )
	turret.GiveWeapon( "mp_weapon_rocket_launcher" )
	DisableWeapons( turret, [] )

	turret.SetDriver( weaponOwner )
	turret.EndSignal( "OnDestroy" )

	// needed to avoid missile gets destroyed and player stuck in turret forever
	AddEntityDestroyedCallback( 
		missile, 
		function( missile ) : ( missile, turret )
		{
			if ( IsValid( turret ) )
				turret.ClearParent() // clear turret so it won't get destroyed
		}
	)

	AddEntityDestroyedCallback( 
		turret, 
		function( turret ) : ( turret, weaponOwner )
		{
			if ( IsValid( turret ) )
				turret.ClearDriver() // clear driver before turret actually gets destroyed
		}
	)

	OnThreadEnd
	(
		function(): ( weapon, weaponOwner, camera, turret, results )
		{
			if ( IsValid( weaponOwner ) )
			{
				// now try to use viewcontrol entity
				//weaponOwner.SetTrackEntity( null )
				//weaponOwner.ClearTrackEntitySettings()

				//weaponOwner.ClearViewEntity()
				DeployAndEnableWeapons( weaponOwner )

				weaponOwner.SetAngles( results.firingStartAngles )
				ScreenFade( weaponOwner, 0, 0, 0, 255, 2, 0.2, (FFADE_IN | FFADE_PURGE) )

				file.playerActiveCruiseMissile[ weaponOwner ] = null

				// if turret accidentally gets destroyed, this is required to make player out of movement lock
				if ( !IsValid( turret ) )
					weaponOwner.Die( weaponOwner, weaponOwner, { damageSourceId = damagedef_suicide } )
			}

			if ( IsValid( weapon ) )
			{
				//weapon.RemoveMod( "disable_reload" )
			}

			if ( IsValid( camera ) )
				camera.Destroy()

			if ( IsValid( turret ) )
			{
				turret.ClearDriver()
				turret.Destroy()
			}
		}
	)

	// can't use "OnDestroy" cause if missile gets destroyed when turret attach on it
	// the player will be stuck forever
	//WaitSignal( missile, "OnDestroy" )
	WaitSignal( missile, "MissileImpact" )

	// manually do impact sound event
	EmitSoundOnEntityOnlyToPlayer( weaponOwner, weaponOwner, "explo_archer_impact_3p" )
}

void function CalculateCruiseMissilePoint( entity weapon, entity weaponOwner )
{
	weaponOwner.EndSignal( "OnDestroy" )
	weaponOwner.EndSignal( "OnDeath" )
	weaponOwner.Signal( "CalculateCruiseMissilePoint" )
	weaponOwner.EndSignal( "CalculateCruiseMissilePoint" )
	weapon.EndSignal( "OnDestroy" )

	entity info_target = CreateEntity( "info_target" )
	info_target.SetOrigin( weapon.GetOrigin() )
	info_target.SetInvulnerable()
	DispatchSpawn( info_target )
	weapon.s.guidedMissileTarget <- info_target

	OnThreadEnd(
		function() : ( weapon, info_target )
		{
			if ( IsValid( info_target ) )
			{
				info_target.Kill_Deprecated_UseDestroyInstead()
				if ( IsValid( weapon ) )
					delete weapon.s.guidedMissileTarget
			}
		}
	)

	while ( true )
	{
		if ( !IsValid_ThisFrame( weaponOwner ) || !IsValid_ThisFrame( weapon ) )
			return

		TraceResults result = GetViewTrace( weaponOwner )
		info_target.SetOrigin( result.endPos )

		WaitFrame()
	}
}

/*
void function CruiseMissilePlayerTrackView( entity player, entity missile )
{
	player.SetTrackEntity( missile ) // Sets an entity to be viewed by this player in third person
	//player.SetTrackEntity( GetNPCArray()[0] )
	player.SetTrackEntityPitchLookMode( "orbit" )
	player.SetTrackEntityYawLookMode( "orbit" )
	player.SetTrackEntityDistanceMode( "scriptOffset" )
	player.SetTrackEntityMinYaw( -180 )
	player.SetTrackEntityMaxYaw( 180 )
	player.SetTrackEntityMinPitch( -89 ) // was -89, which means max pitch view
	player.SetTrackEntityMaxPitch( 89 ) // // was 89, which means max pitch view
	player.SetTrackEntityOffset( < -150.0, 0, 50.0 > )
	//player.SetTrackEntityOffsetDistance( -50.0 )
	//player.SetTrackEntityOffsetHeight( 30.0 )
}
*/
#endif