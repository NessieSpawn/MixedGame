untyped
global function DropPodSpawn1Player_Init

global function SpawnFromDroppod
global function SpawnPlayerInDropPod

struct {
	array< entity > droppods
} file

struct {
	entity intermissionCam
	array<entity> droppodSpawns

} droppod

void function DropPodSpawn1Player_Init()
{
	RegisterSignal( "SpawnPlayerInDropPod" )
	RegisterSignal( "PlayerDroppodImpact" ) // signal for sending info messages

	if ( GetMapName() != "mp_lobby" ) // don't run this in lobby
		AddCallback_EntitiesDidLoad( AddDroppodSpawn )
	//AddSpawnCallback( "info_spawnpoint_droppod_start", AddDroppodSpawn )
	//AddCallback_OnRoundEndCleanup( CleanupSpawningDropPods )
}

void function SpawnFromDroppod( entity player, float destructionTime = 10, bool enableRespawnProtection = false )
{
	thread SpawnFromDroppod_Threaded( player, destructionTime, enableRespawnProtection )
}

void function SpawnFromDroppod_Threaded( entity player, float destructionTime, bool enableRespawnProtection )
{
	if ( !IsValid( player ) )
		return
	if( GetGameState() >= eGameState.Postmatch )
		return
	// can't implement it good enough
	entity spawnpoint = FindPlayerDroppodSpawnPoint( player.GetTeam() )
	SpawnPlayerInDropPod( player, spawnpoint.GetOrigin(), spawnpoint.GetAngles(), destructionTime, enableRespawnProtection )
}

void function CleanupSpawningDropPods()
{
	foreach ( entity pod in file.droppods )
		pod.Destroy()
	
	file.droppods.clear()
}

void function SpawnPlayerInDropPod( entity player, vector targetOrigin, vector angles, float destructionTime = -1, bool enableRespawnProtection = false )
{
	if ( !IsValid( player ) )
		return

	player.EndSignal( "OnDeath" )
	player.EndSignal( "OnDestroy" )
	player.Signal( "SpawnPlayerInDropPod" )
	player.EndSignal( "SpawnPlayerInDropPod" )
	
	svGlobal.levelEnt.EndSignal( "CleanUpEntitiesForRoundEnd" )
	svGlobal.levelEnt.EndSignal( "CleanUpEntitiesForMatchEnd" )

	// some stuffs to setup: enable notsolid, invulnerability and notarget
	// so player won't touch triggers or be damaged
	player.SetInvulnerable()
	player.NotSolid()
	player.SetNoTarget( true )

	table results = {}
	results.droppodLanded <- false
	results.playerLanded <- false // since player's landing will sometimes delay 1 frame(eg. first spawn), have to use another var

	entity pod = CreateDropPod( targetOrigin, angles )
	pod.EndSignal( "OnDestroy" )
	file.droppods.append( pod )

	// add a indicator for team players and player themselves
	vector surfaceNormal = < 0, 0, 1 >
	int index = GetParticleSystemIndex( $"P_ar_titan_droppoint" )
	entity indicator = StartParticleEffectInWorld_ReturnEntity( index, targetOrigin, surfaceNormal )
	EffectSetControlPointVector( indicator, 1, < 255, 255, 255 > ) // white
	indicator.DisableHibernation()
	SetTeam( indicator, player.GetTeam() )
	indicator.SetOwner( player )
	indicator.kv.VisibilityFlags = ENTITY_VISIBLE_TO_OWNER | ENTITY_VISIBLE_TO_FRIENDLY // owner and teammates can see
	
	// TODO: we need to make a door for this, CreateDropPodDoor in _droppod_fireteam is just busted for some reason tho
	entity camera = CreateEntity( "point_viewcontrol" )
	// can be pretty annoying since droppod is spawned outside of the map
	// tried to modify LaunchAnimDropPod() and add sequenceInitialTime, will cause sound issues
	/*
	camera.SetParent( pod, "ORIGIN" )
	camera.SetLocalOrigin( < 0, 0, 800 > + AnglesToForward( angles ) * -400 )
	camera.SetAngles( < 60, angles.y, 0 > )
	*/
	vector podloaction = pod.GetOrigin() + Vector( 0, 0, 2200 ) // was 3000, maybe higher than boomtown's roof
	camera.SetLocalOrigin( podloaction )
	camera.SetAngles( < 90, angles.y, 0 > )

	// all entity creations done, ready to clean up
	OnThreadEnd
	(
		function(): ( results, player, pod, camera, indicator )
		{
			if ( IsValid( player ) )
			{
				if ( !results.playerLanded ) // thread end but player not landed yet? maybe they died
				{
					// clean up these
					player.ClearParent()
					player.ClearViewEntity()
					player.UnfreezeControlsOnServer()
					DeployAndEnableWeapons( player )
					// protection clean up
					player.Solid()
					player.ClearInvulnerable()
					player.SetNoTarget( false )
				}
			}

			if ( IsValid( pod ) )
			{
				if ( !results.droppodLanded ) // pod not landing but player died or something?
					CleanupRespawnDroppod( pod ) // clean it immediately
			}

			if ( IsValid( camera ) )
				camera.Destroy()

			if ( IsValid( indicator ) ) // remove indicator
				EffectStop( indicator )
		}
	)
	
	AddCinematicFlag( player, CE_FLAG_HIDE_MAIN_HUD )
	player.SetViewEntity( camera, true )

	// better not handle player respawn here...
	/*
	if ( !IsAlive( player ) )
	{
		print( "we're respawning player on droppod spawn!" )
		DecideRespawnPlayer( player )
		WaitFrame() // take hold from t-pose
	}
	*/

	HolsterAndDisableWeapons( player ) // holster weapon so they won't have weapon deploy sound out

	// attach pod and camera
	player.SetOrigin( pod.GetOrigin() )
	player.SetAngles( pod.GetAngles() )
	player.SetParent( pod ) // don't use ( pod, "ATTACH", true ), which will cause visual issue while disembarking

	player.FreezeControlsOnServer()
	
	// wait for this
	LaunchAnimDropPod( pod, "pod_testpath", targetOrigin, angles )

	results.droppodLanded = true // mark landing as true
	
	//WaitFrame()
	if( GetGameState() < eGameState.Postmatch ) // anti-crash, should be pretty much well
	{
		player.ClearParent()
		player.ClearViewEntity()
		WaitFrame() // try always wait 1 frame here
		//if ( player.s.respawnCount == 1 ) // hack for first respawn, or player will facing the pod for some reason
		//	WaitFrame()

		if( GetGameState() > eGameState.Prematch ) // make compatiblility with intros
			player.UnfreezeControlsOnServer() // otherwise we unfreeze player in intro

		player.Signal( "PlayerDroppodImpact" )
		RemoveCinematicFlag( player, CE_FLAG_HIDE_MAIN_HUD )
		vector posBeforeFix = player.GetOrigin()
		FindNearestSafeSpotAndPutEntity( player, 1 ) // defensive fix
		if ( player.GetOrigin() != posBeforeFix )
		{
			print( "player droppod landing position fixed!" )
			// update pod position to player's new pos
			pod.Anim_Stop() // stop any droppod sequence pending
			pod.SetAbsOrigin( player.GetOrigin() )
			//player.SetOrigin( pod.GetOrigin() )
		}
		// idk why pods moving everywhere... turns out it needs to set abs origin
		//print( "player pos: " + string( player.GetOrigin() ) )
		//print( "pod pos: " + string( pod.GetOrigin() ) )
		player.SetAbsAngles( angles ) // always facing the spawnpoint. seems SetAbsAngles could handle it

		// give loadout again so they'll have weapon deploy animations
		if ( Loadouts_CanGivePilotLoadout( player ) ) // to fit vanilla: give loadout upon touched ground, so they'll have weapon's first deploy animation
			Loadouts_TryGivePilotLoadout( player )
		else // fix for northstar: we may disabled loadout grade period, should update their weapon to make first deploy animation show up
		{
			StorePilotWeapons( player )
			RetrievePilotWeapons( player )
		}
		DeployAndEnableWeapons( player )

		results.playerLanded = true // mark as player landed
		
		// droppod player respawn protection
		player.Solid()
		if ( enableRespawnProtection )
			thread DroppodPlayerProtectionThink( player, pod, destructionTime )
		else // clean up immediately
		{
			player.ClearInvulnerable()
			player.SetNoTarget( false )
			// normal dissolve think
			if ( destructionTime != -1 )
				thread DelayedCleanUpDroppod( pod, destructionTime ) // start droppod cleanup here
		}
	}
}

void function DroppodPlayerProtectionThink( entity player, entity pod, float maxDuration )
{
	player.EndSignal( "OnDestroy" )
	player.EndSignal( "OnDeath" )
	pod.EndSignal( "OnDestroy" )

	player.EndSignal( "OnPrimaryAttack" )
	player.EndSignal( "OnMelee" ) // nessie modified signal, sent on melee attach active

	// invincibility handled by SpawnPlayerInDropPod(), this function is just for cleaning it up
	//player.SetInvulnerable()

	OnThreadEnd
	(
		function(): ( player, pod )
		{
			if ( IsValid( player ) )
			{
				player.ClearInvulnerable()
				player.SetNoTarget( false )
			}
			if ( IsValid( pod ) )
				CleanupRespawnDroppod( pod )
		}
	)

	float endTime = Time() + maxDuration
	if ( maxDuration == -1 ) // infinite hold time
		endTime = Time() + 65535 // hopefully nobody will stay inside droppod for over 100mins...
		
	vector safeOrigin = player.GetOrigin()
	const float droppodRadius = 50 // hopefully a good enough value

	while ( Time() < endTime )
	{
		WaitFrame()
		if ( Distance( player.GetOrigin(), safeOrigin ) > droppodRadius )
			break
		
		entity activeWeapon = player.GetActiveWeapon()
		if ( IsValid( activeWeapon ) )
		{
			// trying to ads
			if ( player.GetZoomFrac() > 0 )
				break

			// trying to charge up weapon
			if ( activeWeapon.IsChargeWeapon() && activeWeapon.GetWeaponChargeFraction() > 0 )
				break

			// trying to use ability or cook grenade
			// player can have only offhand weapons such as melee... needs to check main weapon count as well
			if ( player.GetMainWeapons().len() > 0 && activeWeapon.IsWeaponOffhand() )
				break

			// trying to lock with weapon
			// maybe we break protection whenever player tried to aim down sight?
			//if ( activeWeapon.GetWeaponSettingBool( eWeaponVar.attack_button_presses_ads ) && player.GetZoomFrac() == 1 )
			//	break
		}
	}
}

void function DelayedCleanUpDroppod( entity pod, float destructionTime )
{
	pod.EndSignal( "OnDestroy" )
	if ( destructionTime > 0 )
		wait destructionTime

	CleanupRespawnDroppod( pod )
}

void function CleanupRespawnDroppod( entity pod )
{
	// basically same as CleanupFireteamPod()
	EmitSoundAtPosition( TEAM_UNASSIGNED, pod.GetOrigin(), "droppod_dissolve" )
	pod.NotSolid() // so it won't block player's movement
	pod.Dissolve( ENTITY_DISSOLVE_CORE, Vector( 0, 0, 0 ), 500 )

	file.droppods.remove( file.droppods.find( pod ) )
}

void function AddDroppodSpawn()
{
	// now adding more spawnpoints
	droppod.droppodSpawns.extend( SpawnPoints_GetTitan() )
	//droppod.droppodSpawns.extend( GetEntArrayByClass_Expensive( "info_spawnpoint_droppod_start" ) )
	// remove titan start points, since grunt mode uses them as titan's spawn point
	// adding back because our new logic no longer always uses start points
	droppod.droppodSpawns.extend( SpawnPoints_GetTitanStart( TEAM_IMC ) )
	droppod.droppodSpawns.extend( SpawnPoints_GetTitanStart( TEAM_MILITIA ) )
	// remove droppod regular points, since grunt mode uses them to spawn npcs
	// adding back because our new logic will update spawn's lastUsedTime
	droppod.droppodSpawns.extend( SpawnPoints_GetDropPod() )
	droppod.droppodSpawns.extend( SpawnPoints_GetDropPodStart( TEAM_IMC ) )
	droppod.droppodSpawns.extend( SpawnPoints_GetDropPodStart( TEAM_MILITIA ) )

	array<entity> pilotSpawns
	pilotSpawns.extend( SpawnPoints_GetPilot() )
	pilotSpawns.extend( SpawnPoints_GetPilotStart( TEAM_IMC ) )
	pilotSpawns.extend( SpawnPoints_GetPilotStart( TEAM_MILITIA ) )
	foreach ( entity point in pilotSpawns )
	{
		vector origin = point.GetOrigin()
		vector roofTraceStart = origin
		vector roofTraceEnd = roofTraceStart + < 0,0,65535 >
		vector lowerDoorTraceStart = origin
		vector lowerDoorTraceEnd = lowerDoorTraceStart + AnglesToForward( < 0,point.GetAngles().y, 0> ) * 30
		vector upperDoorTraceStart = point.GetOrigin() + < 0, 0, 80 >
		vector upperDoorTraceEnd = upperDoorTraceStart + AnglesToForward( < 0,point.GetAngles().y, 0> ) * 30
		vector leftDoorTraceStart = lowerDoorTraceEnd
		vector leftDoorTraceEnd = leftDoorTraceStart - AnglesToRight( < 0, point.GetOrigin().y, 0 > ) * 30
		vector rightDoorTraceStart = lowerDoorTraceEnd
		vector rightDoorTraceEnd = rightDoorTraceStart + AnglesToRight( < 0, point.GetOrigin().y, 0 > ) * 30

		const int traceMask = (TRACE_MASK_SHOT | CONTENTS_BLOCKLOS)
		const int collisionGroup = TRACE_COLLISION_GROUP_NONE

		// if there're nothing blocked within 3000 hu we consider use it as a spawnpoint
		// now we needs roofcheck to hit sky, no only HU checks
		TraceResults roofCheck = TraceLine( roofTraceStart, roofTraceEnd, point, traceMask, collisionGroup )
		// if there're also nothing blocking the door we surely use it as a spawnpoint
		TraceResults lowerDoorCheck = TraceLine( lowerDoorTraceStart, lowerDoorTraceEnd, point, traceMask, collisionGroup )
		TraceResults upperDoorCheck = TraceLine( upperDoorTraceStart, upperDoorTraceEnd, point, traceMask, collisionGroup )
		TraceResults leftDoorCheck = TraceLine( leftDoorTraceStart, leftDoorTraceEnd, point, traceMask, collisionGroup )
		TraceResults rightDoorCheck = TraceLine( rightDoorTraceStart, rightDoorTraceEnd, point, traceMask, collisionGroup )

		//print( "[DROPPOD_RESPAWN] pilot spawn point at: " + string( point.GetOrigin() ) + " roofcheck: " + string( roofCheck.hitEnt ) )
		//print( "[DROPPOD_RESPAWN] pilot spawn point at: " + string( point.GetOrigin() ) + " doorCheck: " + string( doorCheck.hitEnt ) )

		if ( roofCheck.hitSky && !IsValid( lowerDoorCheck.hitEnt ) && !IsValid( upperDoorCheck.hitEnt ) && !IsValid( leftDoorCheck.hitEnt ) && !IsValid( rightDoorCheck.hitEnt ) )
		{
			droppod.droppodSpawns.append( point )
			//print( "[DROPPOD_RESPAWN] found a valid pilot spawn point at: " + string( point.GetOrigin() ) )
		}
	}

	// validate points using normal grunt's navmesh
	ValidateNPCSpawnpoints( droppod.droppodSpawns )

	foreach ( entity point in droppod.droppodSpawns )
	{
		// still better to use global lastUsedTime... I think
		if ( !( "lastUsedTime" in point.s ) )
			point.s.lastUsedTime <- -1
		point.s.droppodLastUsedTime <- -1
		point.s.droppodLastUsedTeam <- TEAM_UNASSIGNED // for better checks?
	}
}

const float SPAWN_ZONE_SEARCH_RADIUS = 1800
const float DROPPOD_POINT_COOLDOWN = 20.0 // increase from 10 because we're now using global spawn lastUsedTime

// should use a better one
entity function FindPlayerDroppodSpawnPoint( int team )
{
	array<entity> spawnPoints = droppod.droppodSpawns
	// modified: make a new function so ai gamemodes don't have to re-decide for each spawn
	// due to spawn points refactor, let's change back to older version
	entity zone = DecideSpawnZone_Generic( spawnPoints, team )
	//entity zone = GetCurrentSpawnZoneForTeam( team )
	
	entity chosenPoint
	if ( IsValid( zone ) )
	{
		// 20 Tries to get a random point close to the zone
		for ( int i = 0; i < 20; i++ )
		{
			entity point = spawnPoints[ RandomInt( spawnPoints.len() ) ]
		
			// still better to use global lastUsedTime... I think
			//if ( Distance2D( point.GetOrigin(), zone.GetOrigin() ) < SPAWN_ZONE_SEARCH_RADIUS && point.s.droppodLastUsedTime + DROPPOD_POINT_COOLDOWN < Time() ) // smaller search
			if ( Distance2D( point.GetOrigin(), zone.GetOrigin() ) < SPAWN_ZONE_SEARCH_RADIUS && point.s.lastUsedTime + DROPPOD_POINT_COOLDOWN < Time() ) // smaller search
			{
				chosenPoint = point
				//print( "[DROPPOD_RESPAWN] found a point which inside spawn zone and off cooldown!" )
				break
			}
		}
	}
	
	if ( !IsValid( chosenPoint ) )
	{
		// 20 Tries to get a random point off cooldown
		for ( int i = 0; i < 20; i++ )
		{
			entity point = spawnPoints[ RandomInt( spawnPoints.len() ) ]

			// still better to use global lastUsedTime... I think
			//if ( point.s.droppodLastUsedTime + DROPPOD_POINT_COOLDOWN < Time() )
			if ( point.s.lastUsedTime + DROPPOD_POINT_COOLDOWN < Time() )
			{
				chosenPoint = point
				//print( "[DROPPOD_RESPAWN] found a point which is off cooldown!" )
				break
			}
		}
	}

	// still have no valid point... use a completely random one
	if ( !IsValid( chosenPoint ) )
	{
		chosenPoint = spawnPoints[ RandomInt( spawnPoints.len() ) ]
		//print( "[DROPPOD_RESPAWN] can't find any valid point! now using a random one" )
	}

	// still better to use global lastUsedTime... I think
	//chosenPoint.s.droppodLastUsedTime = Time()
	chosenPoint.s.lastUsedTime = Time()
	//print( "[DROPPOD_RESPAWN] current respawn point origin: " + string( chosenPoint.GetOrigin() ) )
	return chosenPoint
}